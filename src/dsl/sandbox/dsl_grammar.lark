?start: game?

game: _NL? "GAME" (start_g end_g | end_g start_g) property* room* _NL?

start_g: "START" phrase
end_g: "END" phrase

room: _ROOM id (property | connections)* item* _NL?

item: _ITEM id "IN" location property* _NL?
    | _ITEM id property* _NL?

_ITEM.5: _NL "ITEM"
_ROOM.5: _NL "ROOM"

property: _NL phrase ":" ESCAPED_STRING

connections: _CONNECT connection ("," _NL? connection)*
connection: phrase "TO" phrase
_CONNECT.5: _NL "connections:"

id: phrase

location: phrase

phrase: STRING+

_NL: ( /(\r?\n[\t ]*)/ | COMMENT) +

%import .util.STRING
%import .util.ESCAPED_STRING

%import .util.COMMENT
%ignore COMMENT

%import common.WS_INLINE
%ignore WS_INLINE
